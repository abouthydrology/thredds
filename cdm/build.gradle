apply plugin: 'fatjar'

jar {
    manifest {
        attributes 'Implementation-Title': 'cdm'
    }
}

sourceSets {
    main {
        java {
            exclude 'thredds/catalog2/**/*.java'
            exclude 'ucar/nc2/util/reflect/*.java'
        }
    }
}

dependencies {
  compile project(':udunits')
  compile ('commons-httpclient:commons-httpclient:3.1') {
    transitive = false  // commons-logging
  }
  compile 'com.google.protobuf:protobuf-java:2.5.0'
  compile 'joda-time:joda-time:2.2'
  compile 'net.jcip:jcip-annotations:1.0'
  compile 'net.sf.ehcache:ehcache:1.6.2'
  compile 'org.jdom:jdom2:2.0.4'
  compile ('org.quartz-scheduler:quartz:2.1.5') {   // eliminate c3p0
    transitive = false
  }
  compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
  runtime group: 'org.slf4j', name: 'jcl-over-slf4j', version: slf4jVersion

  testCompile group: 'junit', name: 'junit', version: junitVersion
}

javadoc {
  title = 'Public javadoc for netcdf-java version ' + version
  source = fileTree(dir: 'src/main/java').matching {
    include 'thredds/catalog/*.java'
    include 'ucar/ma2/*.java'
    include 'ucar/nc2/*.java'
    include 'ucar/nc2/constants/*.java'
    include 'ucar/nc2/dataset/**/*.java'
    include 'ucar/nc2/dt/*.java'
    include 'ucar/nc2/dt/grid/*.java'
    include 'ucar/nc2/ft/*.java'
    include 'ucar/nc2/ncml/*.java'
    include 'ucar/nc2/time/*.java'
    include 'ucar/nc2/units/*.java'
    include 'ucar/nc2/util/*.java'
    include 'ucar/unidata/**/*.java'
    include 'ucar/units/*.java'
  }
}

task releaseDocs(dependsOn: javadoc) << {
  releaseDir = webdir + "javadoc"

  ant.delete(dir: releaseDir)
  ant.mkdir(dir: releaseDir)

  copy {
    println "copyDocs to " + releaseDir
    from("build/docs/javadoc")
    into releaseDir
  }
}

/////////

ext {
  cdmJar = 'cdmFat-'+version+'.jar'
}

fatJar {  // does not inherit from parent jar
  archiveName = cdmJar
  manifest {
    attributes 'Implementation-Title': cdmJar,
     'Implementation-Version': version,
     'Implementation-Vendor': 'UCAR/Unidata',
     'Implementation-Vendor-Id': 'edu.ucar',
     'Built-On': new Date(),
     'Main-Class': 'ucar.nc2.NCdumpW'
  }
  exclude 'com/sleepycat/**'
  exclude 'org/quartz/**'
}